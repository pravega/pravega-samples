/*
 * Copyright (c) 2017 Dell Inc., or its subsidiaries. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 */
plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.4'
}

apply plugin: "java"
apply plugin: "scala"
apply plugin: "distribution"
apply plugin: 'idea'
apply plugin: 'eclipse'

sourceCompatibility = "1.8"
archivesBaseName = 'pravega-flink-scenario-turbineheatprocessor'

dependencies {
    compile "org.scala-lang.modules:scala-java8-compat_$flinkScalaVersion:${scalaJava8CompatVersion}"
    compile "io.pravega:pravega-connectors-flink_$flinkScalaVersion:${flinkConnectorVersion}"
    compile "org.apache.flink:flink-streaming-java_$flinkScalaVersion:${flinkVersion}"
    compile "org.apache.flink:flink-streaming-scala_$flinkScalaVersion:${flinkVersion}"
    compile "org.slf4j:slf4j-log4j12:1.7.14"
    if (includePravegaCredentials.toBoolean()) {
        compile "io.pravega:pravega-keycloak-credentials:${pravegaCredentialsVersion}"
    }
}

shadowJar {
    dependencies {
        include dependency("org.scala-lang.modules:scala-java8-compat_$flinkScalaVersion")
        include dependency("io.pravega:pravega-connectors-flink_$flinkScalaVersion")
    }
}

task scriptTrubineHeatProcessor(type: CreateStartScripts) {
    outputDir = file('build/scripts')
    mainClassName = 'io.pravega.turbineheatprocessor.TurbineHeatProcessor'
    applicationName = 'turbineHeatProcessor'
    classpath = files(jar.archivePath) + sourceSets.main.runtimeClasspath
}

distributions {
    main {
        baseName = archivesBaseName
        contents {
            into('lib') {
                from shadowJar
                from(project.configurations.shadow)
                from(jar)
                from(project.configurations.runtime)
            }
            into('bin') {
                from project.scriptTrubineHeatProcessor
            }
        }
    }
}