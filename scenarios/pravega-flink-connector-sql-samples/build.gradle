/*
 * Copyright (c) 2017 Dell Inc., or its subsidiaries. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 */
plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.4'
}

apply plugin: "java"
apply plugin: "scala"
apply plugin: "distribution"
apply plugin: 'idea'
apply plugin: 'eclipse'

sourceCompatibility = "1.8"

archivesBaseName = 'pravega-flink-connector-sql-samples'

dependencies {
    compile "io.pravega:pravega-keycloak-client:${pravegaKeycloakVersion}"

    compile "io.pravega:pravega-connectors-flink-${flinkMajorMinorVersion}_${flinkScalaVersion}:${flinkConnectorVersion}"
    compile "org.apache.flink:flink-streaming-java_${flinkScalaVersion}:${flinkVersion}"
    compile "org.apache.flink:flink-table-planner_${flinkScalaVersion}:${flinkVersion}"
    compile "org.apache.flink:flink-table-api-java-bridge_${flinkScalaVersion}:${flinkVersion}"
    compile "org.apache.flink:flink-streaming-scala_${flinkScalaVersion}:${flinkVersion}"

    compile "ch.qos.logback:logback-classic:1.1.7"
    compile "com.fasterxml.jackson.core:jackson-databind:2.8.9"

    compile "joda-time:joda-time:2.7"
    compile "org.projectlombok:lombok:1.16.18"
}

task scriptConnectorTableApiSamples(type: CreateStartScripts) {
    outputDir = file('build/scripts')
    mainClassName = 'io.pravega.connectors.nytaxi.ApplicationMain'
    applicationName = 'tableapi-samples'
    defaultJvmOpts = ["-Dlog4j.configuration=file:conf/logback.xml"]
    classpath = files(jar.archivePath) + sourceSets.main.runtimeClasspath + sourceSets.main.resources
}

distributions {
    main {
        baseName = archivesBaseName
        contents {
            into('bin') {
                from project.scriptConnectorTableApiSamples
            }
            into('lib') {
                from shadowJar
                from(project.configurations.shadow)
                from(jar)
                from(project.configurations.runtime)
            }
            into('conf') {
                from(project.file('src/main/dist/conf/logback.xml'))
            }
        }
    }
}

shadowJar {
    dependencies {
        include dependency("io.pravega:pravega-connectors-flink_${flinkScalaVersion}")
        include dependency("io.pravega:pravega-keycloak-client:${pravegaKeycloakVersion}")
    }
}