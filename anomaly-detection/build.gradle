
plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.4'
}

apply plugin: "java"
apply plugin: "distribution"
apply plugin: 'application'

sourceCompatibility = "1.8"

mainClassName = 'io.pravega.anomalydetection.ApplicationMain'
applicationDefaultJvmArgs = ["-Dlogging.config=file:conf/logback-cli.xml"]

archivesBaseName = 'pravega-flink-anomaly-detection'

dependencies {
    compile "org.slf4j:slf4j-api:1.7.7"
    compile ("io.pravega:pravega-connectors-flink_2.11:${pravegaVersion}:all")
    compileOnly ("org.apache.flink:flink-streaming-java_2.11:${flinkVersion}")
    compileOnly ("org.apache.flink:flink-streaming-scala_2.11:${flinkVersion}")

    compile "io.pravega:pravega-clients:${pravegaVersion}",
            "io.pravega:pravega-common:${pravegaVersion}"

    compile "org.apache.flink:flink-connector-elasticsearch2_2.11:${flinkVersion}"
}

shadowJar {
    dependencies {
        include dependency("io.pravega:pravega-connectors-flink_2.11:${pravegaVersion}:all")
        include dependency("org.apache.flink:flink-connector-elasticsearch2_2.11:${flinkVersion}")
    }
}

distributions {
    main {
        baseName = archivesBaseName
        contents {
            into('lib') {
                from shadowJar
                from(project.configurations.shadow)
            }
            into('conf') {
                from(project.file('src/main/resources/log4j.properties'))
                from(project.file('src/main/resources/logback.xml'))
                from(project.file('src/main/resources/app.json'))
            }
        }
    }
}

run {
    standardInput = System.in

    if (project.hasProperty("argsList")) {
        args(argsList.split(' '))
    }

    /*
        Ths is how application can be executed using gradle run
        ./gradlew anomaly-detection:run -PargsList="--configDir <CONFIG_PATH> --mode <RUN_MODE>"
     */
}